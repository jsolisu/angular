load("//tools:defaults.bzl", "nodejs_binary", "ts_library")

package(default_visibility = ["//visibility:public"])

BIN_ENTRYPOINTS = [
    "get-base-currencies-file",
    "get-base-locale-file",
    "get-closure-locale-file",
    "write-locale-files-to-dist",
]

ts_library(
    name = "bin",
    srcs = glob(["*.ts"]),
    deps = [
        "//packages/common/locales/generate-locales-tool",
        "@npm//@types/node",
    ],
)

[nodejs_binary(
    name = entrypoint,
    data = [
        ":bin",
        "@cldr_json_data//:all_json",
        "@cldr_xml_data//:all_xml",
    ],
    entry_point = ":%s.ts" % entrypoint,
    # We need to patch the NodeJS module resolution as this binary runs as
    # part of a genrule where the linker does not work as expected.
    # See: https://github.com/bazelbuild/rules_nodejs/issues/2600.
    templated_args = ["--nobazel_run_linker"],
) for entrypoint in BIN_ENTRYPOINTS]
